
model {

  #---------------------------------------------------------------
  #----------------  Type-level/regression priors ----------------
  #------- (same-class rich, same-class poor, mixed-class) -------
  #---------------------------------------------------------------

  #------------------------ alpha priors ------------------------
  a.gamma_alpha <- 1/2
  b.gamma_alpha <- ngroups/3

  phi_alpha ~ dgamma(0.01, 0.01)

  invg_alpha ~ dgamma(a.gamma_alpha, b.gamma_alpha)
  g_alpha <- 1/invg_alpha
  
  vari_alpha <- (g_alpha/phi_alpha) * invSigma
  prec_alpha <- 1/vari_alpha
  
  beta0_alpha ~ dnorm(0, 1) 
  betaX_alpha ~ dnorm(0, prec_alpha)
  
  #------------------------ rho priors ------------------------

  a.gamma_rho <- 1/2
  b.gamma_rho <- ngroups/3
  
  phi_rho ~ dgamma(0.01,0.01)

  invg_rho ~ dgamma(a.gamma_rho, b.gamma_rho)
  g_rho <- 1/invg_rho
  
  vari_rho <- (g_rho/phi_rho) * invSigma
  prec_rho <- 1/vari_rho
  
  beta0_rho ~ dnorm(0, 1)
  betaX_rho ~ dnorm(0, prec_rho)
  
  #----------------  Type-level regressions ----------------

  for (n in 1:ntypes) {
  
    mu_alpha_log[n] <- beta0_alpha + betaX_alpha * X[n] 

    mu_rho_probit[n] <- beta0_rho + betaX_rho * X[n]
    
    
    #--------------------- variance priors ---------------------

    # standard deviation for initial belief (inverse gamma priors)
    tau_alpha[n] ~ dgamma(0.01, 0.01)
    sigma_alpha[n] <- 1/sqrt(tau_alpha[n]) 

    # precision of rate parameters (for beta priors)
    sigma_rho[n] ~ dunif(1, 100)

    # reparameterising gamma prior for beliefs about others on first trial 
    log(mu_alpha[n]) <- mu_alpha_log[n] # standardisation of initial belief mode-log link
    
    rate_alpha[n] <- ( mu_alpha[n] + sqrt( mu_alpha[n]^2 + 4*sigma_alpha[n]^2 ))/
                    (2*sigma_alpha[n]^2) 
    shape_alpha[n] <- 1 + mu_alpha[n] * rate_alpha[n]

    #reparamaterising beta prior for slope of preference CC model
    probit(mu_rho[n]) <- mu_rho_probit[n] # standardisation rate estimate mean-probit link
    shape1_rho[n] <- (mu_rho[n]) * sigma_rho[n]
    shape2_rho[n] <- (1 - mu_rho[n]) * sigma_rho[n]  

  }
  
  #-------------------------------------------------------------------
  #-------------------  Individual level model -----------------------
  #-------------------------------------------------------------------

  for (g in 1:ngroups) {
  
    for (s in 1:groupSize) {

      #--------------------- model priors --------------------- 
      
      alpha[s,g] ~ dgamma(shape_alpha[Type[g]], rate_alpha[Type[g]])T(0.001,)
      rho[s,g] ~ dbeta(shape1_rho[Type[g]], shape2_rho[Type[g]])T(0.001, 0.999) 
  
      # Beliefs about others on first trial - gamma-poisson distribution
      Gb[s,1,g] ~ dpois(alpha[s,g])

      # Contribution preference and actual contribution
      p[s,1,g] <- (rho[s,g] * Gb[s,1,g])
      c[s,1,g] ~ dpois(p[s,1,g])
        
    }
  }
}


